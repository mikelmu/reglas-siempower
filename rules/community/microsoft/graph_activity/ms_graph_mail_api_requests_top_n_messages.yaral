/*
 * Copyright 2025 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

 rule ms_graph_mail_api_requests_top_n_messages {

  meta:
    author = "Google Cloud Security"
    description = "Identify when mailboxes are being accessed with requests for N number of messages"
    rule_id = "mr_4c2e1d84-5569-4bde-a9ad-d747b6fb84bf"
    rule_name = "API access to mailboxes for top N messages via the Microsoft Graph"
    type = "Alert"
    data_source = "MS Graph Activity Logs"
    platform = "Azure"
    severity = "Medium"
    priority = "Medium"

  events:
    $api.metadata.event_type = "NETWORK_HTTP"
    $api.metadata.product_event_type = "Microsoft Graph Activity"
    $api.network.http.method = "GET"
    $api.network.http.response_code = 200
    re.regex($api.target.url, `^https://graph.microsoft.com/v1.0/users/.*/mailFolders/.*\?\$top\=`) nocase
    //Can be tuned for specific User Agent strings being used
    //$api.network.http.user_agent = /PowerShell/ nocase

    $api.principal.ip = $ip
    $api.network.session_id = $session

  match:
    $ip, $session over 5m

  outcome:
    $risk_score = 65
    $event_count = count_distinct($api.metadata.id)
    $requesting_user_guid = array_distinct($api.principal.user.userid)
    $requesting_ip = array_distinct($api.principal.ip)
    $user_agent = array_distinct($api.network.http.user_agent)
    $location = array_distinct($api.principal.location.name)
    $target_application_id_guid = array_distinct($api.target.resource.product_object_id)
    $session_id = array_distinct($api.network.session_id)
    $target_url = array_distinct($api.target.url)
    $victim_user = array_distinct(re.capture($api.target.url, `^https://graph.microsoft.com/v1.0/users/(.*)/mailFolders/.*`))
    $victim_folder = array_distinct(re.capture($api.target.url, `^https://graph.microsoft.com/v1.0/users/.*/mailFolders/(.*)/messages`))
    $messages_requested = max(cast.as_int((re.capture($api.target.url, `^https://graph.microsoft.com/v1.0/users/.*/mailFolders/.*/messages\?\$top\=(\d+)`))))

  condition:
    //Optional threshold to set on the number of emails requested via API
    $api //and $messages_requested >= 100
}
