/*
 * Copyright 2025 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule shimcache_flush {

  meta:
    author = "Florian Roth (Nextron Systems)"
    description = "Detects actions that clear the local ShimCache and remove forensic evidence"
    reference = "https://github.com/SigmaHQ/sigma/blob/master/rules/windows/process_creation/proc_creation_win_rundll32_susp_shimcache_flush.yml"
    license = "https://github.com/SigmaHQ/Detection-Rule-License/blob/main/LICENSE.Detection.Rules.md"
    rule_name = "ShimCache Flush"
    rule_id = "mr_5f6699e1-0518-4664-85aa-9a716f0d4dd6"
    sigma_uuid = "b0524451-19af-4efa-a46f-562a977f792e"
    sigma_status = "stable"
    tactic = "TA0005"
    //tactic = "TA0008"
    //technique = "T1021.001"
    technique = "T1112"
    type = "Detection"
    data_source = "Sysmon"
    platform = "Windows"
    severity = "High"
    priority = "High"
    false_positives = "Unknown"

  events:
    $process.metadata.event_type = "PROCESS_LAUNCH"
    (
        (
            strings.contains(strings.to_lower($process.target.process.command_line), "rundll32") and
            strings.contains(strings.to_lower($process.target.process.command_line), "apphelp.dll")
        )
        and
        (
            strings.contains(strings.to_lower($process.target.process.command_line), "shimflushcache") or
            strings.contains($process.target.process.command_line, "#250")
        )
    )
    or
    (
        (
            strings.contains(strings.to_lower($process.target.process.command_line), "rundll32") and
            strings.contains(strings.to_lower($process.target.process.command_line), "kernel32.dll")
        )
        and
        (
            strings.contains(strings.to_lower($process.target.process.command_line), "baseflushappcompatcache") or
            strings.contains($process.target.process.command_line, "#46")
        )
    )

    $process.principal.hostname = $hostname

  match:
    $hostname over 5m

  outcome:
    //example usage of specifying test user and hostname to adjust risk score
    $risk_score = max(if($process.principal.user.userid = "user" and $process.principal.hostname = "hostname", 0, 15))
    $principal_process_pid = array_distinct($process.principal.process.pid)
    $principal_process_command_line = array_distinct($process.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($process.principal.process.file.sha256)
    $principal_process_file_full_path = array_distinct($process.principal.process.file.full_path)
    $principal_process_product_specfic_process_id = array_distinct($process.principal.process.product_specific_process_id)
    $principal_process_parent_process_product_specfic_process_id = array_distinct($process.principal.process.parent_process.product_specific_process_id)
    $target_process_pid = array_distinct($process.target.process.pid)
    $target_process_command_line = array_distinct($process.target.process.command_line)
    $target_process_file_sha256 = array_distinct($process.target.process.file.sha256)
    $target_process_file_full_path = array_distinct($process.target.process.file.full_path)
    $target_process_product_specfic_process_id = array_distinct($process.target.process.product_specific_process_id)
    $principal_user_userid = array_distinct($process.principal.user.userid)
    $log_type = array_distinct(strings.concat($process.metadata.log_type,"/",$process.metadata.product_event_type))

  condition:
    $process
}
