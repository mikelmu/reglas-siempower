/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

 rule onelogin_application_password_revealed {

  meta:
    author = "Google Cloud Security"
    description = "Detects when a user revealed another user's application password."
    rule_id = "mr_785c2d0e-067e-4ad6-a27b-91d0af644718"
    rule_name = "OneLogin Application Password Revealed"
    reference = "https://onelogin.service-now.com/kb_view_customer.do?sysparm_article=KB0010598"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Unsecured Credentials"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1552/"
    mitre_attack_version = "v15.1"
    type = "Alert"
    data_source = "OneLogin"
    severity = "High"
    priority = "High"

  events:
    $password.metadata.event_type = "USER_RESOURCE_ACCESS"
    $password.metadata.product_name = "ONELOGIN_SSO"
    $password.metadata.vendor_name = "ONELOGIN"
    $password.principal.user.userid != $password.target.user.userid //different users
    $password.metadata.product_event_type = "240" //actor_user revealed password to an app for user
    $password.principal.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $risk_score = max(75)
    $mitre_attack_tactic = "Credential Access"
    $mitre_attack_technique = "Unsecured Credentials"
    $principal_ip = array_distinct($password.principal.ip)
    $principal_ip_country = array_distinct($password.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($password.principal.ip_geo_artifact.location.state)
    $principal_user_userid = array_distinct($password.principal.user.userid)
    $principal_user_user_display_name = array_distinct($password.principal.user.user_display_name)
    $target_user_userid = array_distinct($password.target.user.userid)
    $target_user_user_display_name = array_distinct($password.target.user.user_display_name)
    $target_application = array_distinct($password.target.application)

  condition:
    $password
}
