/*
 * Copyright 2021 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule mitre_attack_T1547_001_windows_registry_run_keys_startup_folder
{
  meta:
    author = "Google Cloud Security"
    description = "Creation or changes of the registry AutoStart Execution Point (ASEP) / run keys"
    reference = "https://attack.mitre.org/techniques/T1547/001/"
    yara_version = "YL2.0"
    rule_version = "1.0"

  events:
    (
      $e1.metadata.event_type = "REGISTRY_CREATION" or
      $e1.metadata.event_type = "REGISTRY_MODIFICATION"
    )
    and
    (
      re.regex($e1.target.registry.registry_key, `(HKCU|HKEY_CURRENT_USER)\\Software\\Microsoft\\Windows\\CurrentVersion\\(Run|RunOnce)`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKCU|HKEY_CURRENT_USER)\\Software\\Microsoft\\Windows\\CurrentVersion\\Explorer\\(User )?Shell Folders`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKCU|HKEY_CURRENT_USER)\\Software\\Microsoft\\Windows\\CurrentVersion\\Policies\\Explorer\\Run`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKCU|HKEY_CURRENT_USER)\\Software\\Microsoft\\Windows\\CurrentVersion\\RunServices(Once)?`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKCU|HKEY_CURRENT_USER)\\Software\\Microsoft\\Windows NT\\CurrentVersion\\Windows`) nocase or

      re.regex($e1.target.registry.registry_key, `(HKLM|HKEY_LOCAL_MACHINE)\\Software\\Microsoft\\Windows\\CurrentVersion\\(Run|RunOnce|RunOnceEx)`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKLM|HKEY_LOCAL_MACHINE)\\Software\\Microsoft\\Windows\\CurrentVersion\\Explorer\\(User )?Shell Folders`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKLM|HKEY_LOCAL_MACHINE)\\Software\\Microsoft\\Windows\\CurrentVersion\\Policies\\Explorer\\Run`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKLM|HKEY_LOCAL_MACHINE)\\Software\\Microsoft\\Windows\\CurrentVersion\\RunServices(Once)?`) nocase or
      re.regex($e1.target.registry.registry_key, `(HKLM|HKEY_LOCAL_MACHINE)\\Software\\Microsoft\\Windows NT\\CurrentVersion\\Winlogon\\(Shell|Userinit)`) nocase or

      re.regex($e1.target.registry.registry_key, `(HKLM|HKEY_LOCAL_MACHINE)\\System\\CurrentControlSet\\Control\\Session Manager`) nocase
    )

  condition:
    $e1
}
